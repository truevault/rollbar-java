buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
  }
}

ext.deps = [
    junit  : '4.12',
    jackson: '2.7.4',
    slf4j  : '1.7.21'
]

subprojects { project ->
  apply plugin: 'java'

  sourceCompatibility = JavaVersion.VERSION_1_8

  repositories {
    jcenter()
  }

  dependencies {
    testCompile "junit:junit:$deps.junit"
    compile 'com.google.code.findbugs:jsr305:3.0.1'
  }

  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.bintray'

  group 'com.truevault.rollbar'
  version '0.6.1'

  task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
  }

  publishing {
    publications {
      bintray(MavenPublication) {
        from components.java
        groupId project.group
        artifactId project.name
        version project.version

        artifact sourceJar {
          classifier 'sources'
        }

        artifact javadocJar {
          classifier 'javadoc'
        }
      }
    }
  }

  bintray {
    user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
    key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
    publications = ['bintray']

    pkg {
      repo = 'maven'
      userOrg = 'truevault'
      licenses = ['MIT']
      vcsUrl = 'https://github.com/truevault/rollbar-java'
      name = project.group

      version {
        name = project.version
        released = new Date()
        vcsTag = 'v' + project.version
      }
    }
  }
}

