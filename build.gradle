buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
  }
}

ext.deps = [
    junit: '4.12',
    gson : '2.3.1'
]

subprojects {
  apply plugin: 'java'

  sourceCompatibility = JavaVersion.VERSION_1_8

  repositories {
    jcenter()
  }

  dependencies {
    testCompile "junit:junit:$deps.junit"
  }
}

['rollbar', 'rollbar-payload', 'rollbar-sender', 'rollbar-testing'].each { projName ->
  project(projName) { project ->
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group 'com.truevault.rollbar'
    version '1.0.0-SNAPSHOT'

    task sourceJar(type: Jar, dependsOn: classes) {
      from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
      from javadoc.destinationDir
    }

    publishing {
      publications {
        bintray(MavenPublication) {
          from components.java
          groupId project.group
          artifactId project.name
          version project.version

          artifact sourceJar {
            classifier 'sources'
          }

          artifact javadocJar {
            classifier 'javadoc'
          }
        }
      }
    }

    bintray {
      user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
      key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
      publications = ['bintray']

      pkg {
        repo = 'maven'
        userOrg = 'truevault'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/truevault/rollbar-java'
        name = project.group

        version {
          name = project.version
          released = new Date()
          vcsTag = 'v' + project.version
        }
      }
    }
  }
}
